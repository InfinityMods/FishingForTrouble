//Red Guards - condition to go red
IF
  Global("ys_GarrisonFight","GLOBAL",1)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
    Continue()
END

//When I'm killed update the total number of dead Red Guards
IF
  Dead("ysredgrd")
  Global("ys_CheckDead","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("ys_DeadRedGuards","GLOBAL",1)
    SetGlobal("ys_CheckDead","LOCALS",1)
END

//Find a target
IF
  Allegiance(Myself,ENEMY)
  OR(2)
		See([GOODCUTOFF])
		See([NEUTRAL])
THEN
	RESPONSE #100
		Continue()
END

//If I'm being attacked then select my attacker as a new target
IF
  Allegiance(Myself,ENEMY)
	OR(3)
		AttackedBy([ANYONE],DEFAULT)
		AttackedBy([ANYONE],MELEE)
		AttackedBy([ANYONE],RANGED)
	See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		Continue()
END

//Select weapon
IF
  Allegiance(Myself,ENEMY)
	!Range(LastSeenBy(Myself),5)
	!Class(LastSeenBy(),INNOCENT)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
	Range(LastSeenBy(Myself),5)
	!Class(LastSeenBy(),INNOCENT)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Attack(LastSeenBy(Myself))
END
