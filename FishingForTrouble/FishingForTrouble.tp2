BACKUP ~FishingForTrouble/backup~
SUPPORT ~www.shsforums.net/forum/550-fishing-for-trouble/~
VERSION ~v3.2.9~

AUTO_TRA ~%MOD_FOLDER%/language/%s~

ALWAYS
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET wmp_xCoord_SoA = 0
		OUTER_SET wmp_yCoord_SoA = 2283
	END ELSE BEGIN
		OUTER_SET wmp_xCoord_SoA = 0
		OUTER_SET wmp_yCoord_SoA = 0
	END

	/* Converts tra files to UTF-8 */
	ACTION_DEFINE_ARRAY noconvert BEGIN END

	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY reload BEGIN Setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/Language~
			noconvert_array = noconvert
			reload_array = reload
	END
END

LANGUAGE ~English~ ~english~ ~FishingForTrouble/language/english/english.tra~
LANGUAGE ~Russian (by subzero400, Velv, minor updates by AGKanevchev)~ ~russian~ ~FishingForTrouble/language/russian/russian.tra~

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//Fishing for Trouble by Yovaneth
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
BEGIN @20000 LABEL ~FishingForTrouble-Main~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @30000

//##################################################
//Apply IDS and EXE patches if necessary (no ToBEx)
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/patches.tpa~

//##################################################
// Copy new material
//##################################################
//Spells
COPY ~%MOD_FOLDER%/spells/yselmin.spl~ ~override~
    SAY NAME1 @149

COPY ~%MOD_FOLDER%/spells/ysghoul.spl~ ~override~

//##################################################
//Cutscenes
//##################################################
//Cerendor Hold
COMPILE ~%MOD_FOLDER%/cutscenes/yscutgte.baf~
COMPILE ~%MOD_FOLDER%/cutscenes/yscut01.baf~
COMPILE ~%MOD_FOLDER%/cutscenes/yscut02.baf~
COMPILE ~%MOD_FOLDER%/cutscenes/yscut03.baf~

//##################################################
//Bams
//##################################################
COPY ~%MOD_FOLDER%/bams~ ~override~

//##################################################
//Music
//##################################################
COPY ~%MOD_FOLDER%/audio/ysblank.mus~ ~music~
COPY_EXISTING ~songlist.2da~ ~override~
	SET_2DA_ENTRY 0 2 3 ~YSBLANK.MUS~ // "no_music" resource

//##################################################
//Sounds
//##################################################
	COPY ~%MOD_FOLDER%/sounds~ ~override~

//##################################################
//Ghoul portrait
//##################################################
	COPY ~%MOD_FOLDER%/bmp~ ~override~

//##################################################
//2da
//##################################################
COPY_EXISTING ~itemdial.2da~ ~override~
	BUT_ONLY

APPEND ~itemdial.2da~ ~YSPRPT		4978	YSPRPT~
	UNLESS ~YSPRPT[ %TAB%]4978[ %TAB%]YSPRPT~
APPEND ~itemdial.2da~ ~YSLABRD		4978	YSLABRD~
	UNLESS ~YSLABRD[ %TAB%]4978[ %TAB%]YSLABRD~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~racetext.2da~ BEGIN
	COPY ~%MOD_FOLDER%/2da/racetext.2da~ ~override~
END ELSE ACTION_IF ((FILE_EXISTS ~TobEx_ini/TobExCore.ini~) AND (FILE_CONTAINS ~TobEx_ini/TobExCore.ini~ ~Externalise Race Selection StrRef=1~)) BEGIN
	APPEND ~racetext.2da~ ~108	15946~ UNLESS ~^108~
	COPY_EXISTING ~racetext.2da~ ~override~
		REPLACE_TEXTUALLY EXACT_MATCH ~108  6315~ ~108  15946~ // Lower Case
END ELSE ACTION_IF (FILE_EXISTS_IN_GAME ~racetext.2da~ AND GAME_IS ~bgee bg2ee eet~) BEGIN
	APPEND ~racetext.2da~ ~GHOUL   108        15946       -1         6315          -1~ UNLESS ~^GHOUL~
END

//##################################################
//Items
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/items.tpa~

//##################################################
//Dialog files
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/dialog.tpa~

//##################################################
//Areas
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/areas.tpa~

//##################################################
//Actor scripts
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/actor_scripts.tpa~

//##################################################
//Area scripts
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/area_scripts.tpa~

//##################################################
//Other area scripts
//##################################################
EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/area_scripts/ysbaldur.baf~ EVALUATE_BUFFER

//##################################################
//Stores
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/stores.tpa~

//##################################################
//Uncompressed Tilesets
//##################################################
COPY ~%MOD_FOLDER%/tis~ ~override~

ACTION_IF GAME_IS ~eet bg2ee~ BEGIN
    COPY ~%MOD_FOLDER%/tis_ee~ ~override~
END

//##################################################
//Worldmap and icons patching - do this last so we can patch the new areas too
//##################################################
INCLUDE ~%MOD_FOLDER%/lib/patch_worldmap.tpa~

//##################################################
// .ogg and .tiz conversion
//##################################################
LAF HANDLE_AUDIO END // audio folder
LAF HANDLE_TILESETS END // tiz folder

// End of core component

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//Fishing for Trouble - Major character portraits
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
BEGIN @20001 LABEL ~FishingForTrouble-Portraits~
REQUIRE_PREDICATE MOD_IS_INSTALLED ~FishingForTrouble.tp2~ (ID_OF_LABEL ~FishingForTrouble.tp2~ ~FishingForTrouble-Main~) @30001

COPY ~%MOD_FOLDER%/portraits~ ~override~
