//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Append mastarea.2da with the new areas
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	APPEND ~MASTAREA.2da~ ~ys0360 value~ UNLESS ~ys0360~	//Cerendor Hold
	APPEND ~MASTAREA.2da~ ~ys0370 value~ UNLESS ~ys0370~	//Cloudpeak Mountains
	APPEND ~MASTAREA.2da~ ~ys0410 value~ UNLESS ~ys0410~	//Imradim
	APPEND ~MASTAREA.2da~ ~ys0390 value~ UNLESS ~ys0390~	//Government District Westside
	APPEND ~MASTAREA.2da~ ~ys1000 value~ UNLESS ~ys1000~	//Portpentyrch
	APPEND ~MASTAREA.2da~ ~ys1001 value~ UNLESS ~ys1001~	//Portpentyrch

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Add new icon
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET YS0370_icon = 98
		OUTER_SET YS0390_icon = 182
		OUTER_SET include_saves = 0
	END ELSE ACTION_IF GAME_IS ~bg2ee~ BEGIN
		INCLUDE ~%MOD_FOLDER%/Worldmap/icon/ee/add_map_icons.tpa~
		LAF ADD_MAP_ICONS STR_VAR path_to_icons = EVAL ~%MOD_FOLDER%/Worldmap/icon/ee/icon.bam~ patch_to_pvrz = EVAL ~%MOD_FOLDER%/Worldmap/icon/ee~ RET new_icon_index END
		OUTER_SET YS0370_icon = 17
		OUTER_SET YS0390_icon = new_icon_index
		OUTER_SET include_saves = 0
	END ELSE BEGIN //bg2, bgt
		INCLUDE ~%MOD_FOLDER%/Worldmap/icon/bg/add_map_icons.tpa~
		LAF ADD_MAP_ICONS STR_VAR path_to_icons = EVAL ~%MOD_FOLDER%/Worldmap/icon/bg/icon.bam~ RET new_icon_index END
		OUTER_SET YS0370_icon = 17
		OUTER_SET YS0390_icon = new_icon_index
		OUTER_SET include_saves = 1
	END

	// patch areas_bg2.tbl to use new dynamic icon
	COPY ~%MOD_FOLDER%/Worldmap/areas_bg2.tbl~ ~%MOD_FOLDER%/Worldmap~
		REPLACE_TEXTUALLY ~\([ %TAB%]+YS0370[ %TAB%]+[0-9]+[ %TAB%]+\)[0-9]+~ ~\1%YS0370_icon%~
		REPLACE_TEXTUALLY ~\([ %TAB%]+YS0390[ %TAB%]+[0-9]+[ %TAB%]+\)[0-9]+~ ~\1%YS0390_icon%~
	BUT_ONLY

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// TBL Worldmap patching
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	INCLUDE ~%MOD_FOLDER%/lib/add_worldmap_tbl.tpa~
	LAF ADD_WORLDMAP_TBL
		INT_VAR
		//verbose = 1
		inclSv = include_saves
		add_x = wmp_xCoord_SoA
		add_y = wmp_yCoord_SoA
		STR_VAR
		path_to_areas = EVAL ~%MOD_FOLDER%/Worldmap/areas_bg2.tbl~
		path_to_areas_bp = EVAL ~%MOD_FOLDER%/Worldmap/areas.tbl~
		path_to_links = EVAL ~%MOD_FOLDER%/Worldmap/links.tbl~
		path_to_trans = EVAL ~%MOD_FOLDER%/language/%LANGUAGE%/worldmap.tra~
	END

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// BP-BGT Worldmap support
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    MKDIR ~Worldmap~
    ACTION_IF FILE_EXISTS ~Worldmap/map_mods_areas.tbl~ BEGIN
        COPY ~Worldmap/map_mods_areas.tbl~ ~Worldmap~ APPEND_FILE ~%MOD_FOLDER%/Worldmap/areas.tbl~
    END ELSE BEGIN
        COPY ~%MOD_FOLDER%/Worldmap/areas.tbl~ ~Worldmap/map_mods_areas.tbl~
    END
    ACTION_IF FILE_EXISTS ~Worldmap/map_mods_links.tbl~ BEGIN
        COPY ~Worldmap/map_mods_links.tbl~ ~Worldmap~ APPEND_FILE ~%MOD_FOLDER%/Worldmap/links.tbl~
    END ELSE BEGIN
        COPY ~%MOD_FOLDER%/Worldmap/links.tbl~ ~Worldmap/map_mods_links.tbl~
    END
    ACTION_IF FILE_EXISTS ~Worldmap/map_mods_trans.tra~ BEGIN
        COPY ~Worldmap/map_mods_trans.tra~ ~Worldmap~ APPEND_FILE ~%MOD_FOLDER%/language/%LANGUAGE%/worldmap.tra~
    END ELSE BEGIN
        COPY ~%MOD_FOLDER%/language/%LANGUAGE%/worldmap.tra~ ~Worldmap/map_mods_trans.tra~
    END
